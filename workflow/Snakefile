import pandas as pd
import os


configfile: "config/config.yml"


localrules:
    multiqc,
    all,


# Read sample list
sample_df = pd.read_csv(config["sample_list"], index_col=0)
samples = sample_df.to_dict(orient="index")

# Read mapping list
mapping_df = pd.read_csv(config["mappings_list"], index_col=0)
mappings = mapping_df.to_dict(orient="index")


rule all:
    """Workflow pseudo rule"""
    input:
        expand(
            "{out_folder}/results/{mapping}/{sample}_fwd.fastq",
            sample=samples.keys(),
            mapping=mappings.keys(),
            out_folder=config["out_folder"],
        ),


rule indexing:
    conda:
        "envs/mapping.yaml"
    input:
        ref=lambda wildcards: mappings[wildcards.map_name]["reference"],
    output:
        idx="{out_folder}/results/mappings/{map_name}/index",
    log:
        log="{out_folder}/results/mappings/{map_name}/log/index/run.log",
        env="{out_folder}/results/mappings/{map_name}/log/index/run_env.yaml",
        settings="{out_folder}/results/mappings/{map_name}/log/run_settings.yaml",
    params:
        mappings_file=config["mappings_list"],
        temp_folder=config["temp_folder"]
        if not config["temp_folder"].startswith("$")
        else os.env(config["temp_folder"].rstrip("$")),
    threads: 24
    shell:
        """
        python workflow/scripts/indexing.py {params.mappings_file} {wildcards.out_folder} {params.temp_folder} {threads} {wildcards.map_name} {log.log}
        """


rule mapping:
    output:
        fwd="{out_folder}/results/mappings/{map_name}/{sample}_fwd.fastq",
        rev="{out_folder}/results/mappings/{map_name}/{sample}_rev.fastq",
        unp="{out_folder}/results/mappings/{map_name}/{sample}_unp.fastq",
    log:
        log="{out_folder}/results/mappings/{map_name}/log/{sample}/run.log",
        fastp_log="{out_folder}/results/mappings/{map_name}/log/{sample}/fastp.log",
        fastp_json="{out_folder}/results/mappings/{map_name}/log/{sample}/fastp.json",
        env="{out_folder}/results/mappings/{map_name}/log/{sample}/run_env.yaml",
        settings="{out_folder}/results/mappings/{map_name}/log/{sample}/run_settings.yaml",
    input:
        idx="{out_folder}/results/mappings/{map_name}/index",
        R1=lambda wildcards: sorted(
            [x for x in samples[wildcards.sample]["fwd_libs"].split(";")]
        ),
        R2=lambda wildcards: sorted(
            [x for x in samples[wildcards.sample]["rev_libs"].split(";")]
        ),
    conda:
        "envs/mapping.yaml"
    threads: 24
    params:
        temp_folder=config["temp_folder"]
        if not config["temp_folder"].startswith("$")
        else os.env(config["temp_folder"].rstrip("$")),
        complexity_threshold=config["fastp"]["complexity_threshold"],
        sample_file=config["sample_list"],
        mappings_file=config["mappings_list"],
    shell:
        """
        python workflow/scripts/mapping.py {params.sample_file} {params.mappings_file} {wildcards.out_folder} {params.temp_folder} {threads} {wildcards.sample} {wildcards.map_name} {log.log}
        """


rule fastp:
    output:
        R1="results/fastp/{sample}.fastp.R1.fastq.gz",
        R2="results/fastp/{sample}.fastp.R2.fastq.gz",
    log:
        log="logs/fastp/{sample}.fastp.log",
        json="logs/fastp/{sample}.fastp.json",
    input:
        R1=lambda wildcards: sorted(
            [x for x in samples[wildcards.sample]["fwd_libs"].split(";")]
        ),
        R2=lambda wildcards: sorted(
            [x for x in samples[wildcards.sample]["rev_libs"].split(";")]
        ),
    envmodules:
        "bioinfo-tools",
        "fastp/0.23.2",
    conda:
        "envs/fastp.yml"
    resources:
        runtime=60 * 10,
    threads: 10
    params:
        tmpR1="$TMPDIR/{sample}.R1.fastq.gz",
        tmpR2="$TMPDIR/{sample}.R2.fastq.gz",
        outR1="$TMPDIR/{sample}.fastp.R1.fastq.gz",
        outR2="$TMPDIR/{sample}.fastp.R2.fastq.gz",
        complexity_threshold=config["fastp"]["complexity_threshold"],
    shell:
        """
        cat {input.R1} > {params.tmpR1}
        cat {input.R2} > {params.tmpR2}
        fastp --thread {threads} -y -Y {params.complexity_threshold} \
            -i {params.tmpR1} -I {params.tmpR2} -o {params.outR1} \
            -O {params.outR2} -j {log.json} > {log.log} 2>&1
        mv {params.outR1} {output.R1}
        mv {params.outR2} {output.R2}
        rm {params.tmpR1} {params.tmpR2}
        """


rule multiqc:
    output:
        "results/multiqc/multiqc.html",
        directory("results/multiqc/multiqc_data"),
    log:
        "logs/multiqc/multiqc.log",
    input:
        fastp=expand("logs/fastp/{sample}.fastp.json", sample=samples.keys()),
    conda:
        "envs/multiqc.yml"
    envmodules:
        "bioinfo-tools",
        "MultiQC/1.12",
    params:
        tmpdir="$TMPDIR/biodivcao",
        outdir=lambda wildcards, output: os.path.dirname(output[0]),
    shell:
        """
        mkdir -p {params.tmpdir}
        cp {input} {params.tmpdir}
        multiqc -f -o {params.outdir} -n multiqc.html {params.tmpdir} > {log} 2>&1
        rm -rf {params.tmpdir}
        """
